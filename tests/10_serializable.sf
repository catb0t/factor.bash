#! ruby
# include ABCs
# import ABCs::Serializable
include lib::ABCs
import lib::ABCs::Serializable

class Tiny (field = "") < Serializable {
  has Hash serial_properties = Hash(
    :field => String
  )
}

class RawProps (p2 = Tiny(), p3 = Hash()) < Serializable {
  has Hash serial_properties = Hash(
    :p1 => String,
    :p2 => Tiny,
    :p3 => Hash,
  )
  has String p1 = "default p1"
}

class Accessors (String _prop1 = '', String _prop2 = '', Hash _prop_hash = Hash()) < Serializable {
  has Hash serial_properties = Hash(
    :prop1 => String,
    :prop2 => String,
    # this property is a raw hash
    :prop_hash => Hash,
  )

  -> prop1 () -> String { self._prop1 }
  -> prop2 () -> String { self._prop2 }
  -> prop_hash () -> Hash { self._prop_hash }

  -> prop1 (String x) { self._prop1 = x; self }
  -> prop2 (String x) { self._prop2 = x; self }
  -> prop_hash (Hash x) { self._prop_hash = x; self }
}

class TestSerial < Serializable {
  has Hash serial_properties = Hash(
    :a => String,
    :acc => Accessors,
    :rp => RawProps,
  )

  # builtin type, not serializable
  has String _a = ""
  # this serializable property uses setter accessors
  has Accessors _acc = Accessors()
  # this serializable property uses bare property assignment
  has RawProps _rp = RawProps()

  -> a () -> String { self._a }
  -> a (Str x) { self._a = x; self }

  -> acc () -> Accessors { self._acc }
  -> acc (Accessors a) { self._acc = a; self }

  -> rp () -> RawProps { self._rp }
  -> rp (RawProps r) { self._rp = r; self }
}

# const eval_scope = { eval(_) }

var first_ts = TestSerial() \
  .a("asd") \
  .acc( Accessors(_prop1: "abc", _prop2: "def", _prop_hash: Hash(1 => 2)) ) \
  .rp( RawProps(p2: Tiny(field: "p2"), p3: Hash(:p => 3) ) ) \

const Hash h1 = first_ts.serialize
# say ts.dump
# say s
var second_ts = TestSerial().unserialize(h1)
say (first_ts.dump == second_ts.dump) # ==> true
assert_eq(first_ts.dump, second_ts.dump) # ==> true

var J = "JSON::PP".require
var j = J.new.pretty # .convert_blessed

var json = j.encode(h1)
# say json
var h2 = j.decode(json)
# say h2
var roundtrip_ts = TestSerial().unserialize(h2)
#
say h2==h1
assert_eq(h2, h1)
say (roundtrip_ts.dump==first_ts.dump)
assert_eq(roundtrip_ts.dump, first_ts.dump)
say (roundtrip_ts.dump==second_ts.dump)
assert_eq(roundtrip_ts.dump, second_ts.dump)
