#! ruby
#! sidef -WkP8
#`(exit the shell) {
  eval 'exec sidef -WkP8 $0 ${1+"$@"}'
}
##  shell_words, part of MultiFactor
##    Copyright Cat Stevens 2018
##  This program is free software: you can redistribute it and/or modify
##    it under the terms of the GNU General Public License as published by
##    the Free Software Foundation, either version 3 of the License, or
##    (at your option) any later version.
##
##    This program is distributed in the hope that it will be useful,
##    but WITHOUT ANY WARRANTY; without even the implied warranty of
##    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
##    GNU General Public License for more details.
##
##    You should have received a copy of the GNU General Public License
##    along with this program.  If not, see <https://www.gnu.org/licenses/>.

include lib::command
include Command

# `build.sh` bash functions
# THEIR utilities, re-implemented in Sidef
module BuildConst {
  define Regex VERSION_REGEX = /VERSION = ([0-9]+\.[0-9]+)/
  define Regex REPRODUCIBLE_REGEX = /REPRODUCIBLE/

  define File GNU_MAKEFILE = %f(GNUmakefile)
  define File N_MAKEFILE = %f(Nmakefile)

  define String NOT_A_GIT_REPO = '^~not a git repository~^'
  const Array NOT_GIT_SYMS = [ BuildConst::NOT_A_GIT_REPO[0..2] ]

  # branchname and file name
  define BASE_REMOTE_IMAGE_URL = 'http://downloads.factorcode.org/images/%s/%s'
  define CHECKSUM_FILENAME = 'checksums.txt'
}

class BuildSh {
  -> init { die "#{__CLASS_NAME__}: don't instantiate me, only use my methods statically" }
  -> downloader {
    static const LWP_UA = 'LWP::UserAgent'.require
    LWP_UA.new(show_progress => 1)
  }
  -> make_http_request (url) {
    Combi::ifte(
      BuildSh.downloader.get(url),
      { .is_success }
      { .decoded_content }
      { false }
    )
  }
  -> find_programs (String kind, Array ps) is cached -> String {
    STDERR.say("Look for '#{kind}': #{ps.join(', ')}...")
    for p in ps {
      const s = `which #{p.quotemeta}`.chomp
      if (Number($?) == 0) {
        STDERR.say("    found #{s}")
        return s
      }
    }
    die "no #{kind}"
  }
  # TODO: not *required*
  # binaries and images aren't compatible across compiler versions though
  # -> check_cc_version (String name) is cached -> Bool {
  #   Bool(name)
  #   ...
  # }
  -> cc () is cached -> String {
    BuildSh.find_programs(:cc, %w(clang gcc cc))
  }
  -> cxx () is cached -> String {
    BuildSh.find_programs(:cxx, %w(clang++ g++ c++ cpp))
  }
  # NOTE: os_type is distinct to osname
  -> os_name (n = nil) is cached -> String {
    given (n || Sys.osname) {
      when (/(?:windows|cygwin)/i) { :windows }
      when (/(?:macos|darwin)/)  { :macos }
      when (/(?:linux|mingw)/)  { :linux }
      when (/(?:bsd|haiku|android|ios)/i) { _ }
      default { die "Unknown osname: #{Sys.osname}" }
    }
  }
  -> os_type (t = nil) is cached -> String {
    given ( t || BuildSh.os_name ) {
      when (/(?:linux|darwin|macos|bsd|mingw)/i) { :unix }
      when (/(?:windows|cygwin)/i) { :windows }
      when (/(?:haiku|android|ios)/i) { _ }
      default { die "Can't convert osname to os_type: #{Sys.osname}" }
    }
  }
  -> arch_name () is cached -> String {
    static const uname = `uname -m`.chomp
    given (uname) {
      case ( [:ppc64, "Power Macintosh"].any{ uname.contains(_) } ) { :ppc }
      when ( /a(?:rm|arch)64/i ) { :arm64 }
      # EABI would be Factor on Bare Metal which isn't in the near future
      when ( /armv7/i ) { :armhf }
      default { :x86 }
    }
  }
  -> c_word_size () is cached -> String {
    define File ws_binary = %f(factor_word_size)
    `echo '#include <stdio.h>
    int main(){printf(\"%ld\",(long)(8*sizeof(void*)));}' | #{BuildSh.cc} -xc -o#{ws_binary} -`

    die "couldn't call the C compiler: #{$!}" \
      if (Number($?) != 0)

    # NOTE: can't be const, or else the value will be discarded
    var String ws = `./#{ws_binary}`
    if (Number($?) != 0) {
      var e = $!
      ws_binary.remove
      die "couldn't run the word size program: #{e}"
    }
    ws_binary.remove
    ws
  }
  -> intel_macosx_word_size () is cached -> String {
    `#{BuildSh.find_programs(:sysctl, [:sysctl])} machdep.cpu.extfeatures | grep EM64T >/dev/null`
    (Number($?)==0 ? :64 : :32)
  }
  -> word_size () is cached -> String {
    if (BuildSh.os_name~~:macos && BuildSh.arch_name~~:x86) {
      BuildSh.intel_macosx_word_size
    } else {
      BuildSh.c_word_size
    }
  }

  -> generate_make_targets () is cached -> Hash {
    # const (
    static const ot = BuildSh.os_type
    static const on = BuildSh.os_name
    static const an = BuildSh.arch_name
    static const ws = BuildSh.word_size
    # )
    Hash(
      :make_image => "#{on}-#{an}.#{ws}",                  # MAKE_IMAGE_TARGET
      :make_vm => "#{on}-#{an}-#{ws}",                     # MAKE_TARGET
      :boot_image_base => "#{ot}-#{an}.#{ws}",             # boot image base (no analogue)
      :boot_image => File("boot.#{ot}-#{an}.#{ws}.image"), # BOOT_IMAGE
    )
  }

  # lots of things aren't allowed in a git branchname
  # see `man git check-ref-format` and https://stackoverflow.com/a/3651867/4532996
  # we're going to use just ~ and ^ internally to identify non-git strings
  -> is_git_ref_name (Str b) is cached {
    b!=BuildConst::NOT_A_GIT_REPO && !( BuildConst::NOT_GIT_SYMS.any{ b.contains(_) } )
  }

  # check the outputs of these with is_git_ref_name:

  # returning `nil` from these is not an option, because `nil` is an empty string
  -> git_branch () is cached {
    static const branch = `git rev-parse --abbrev-ref HEAD`
    (Number($?)==0 ? branch.chomp : BuildConst::NOT_A_GIT_REPO)
  }

  -> git_hash () is cached {
    static const hash = `git rev-parse HEAD`
    (Number($?)==0 ? hash.chomp : BuildConst::NOT_A_GIT_REPO)
  }

  -> try_git (Str operation, Str caller_name, Bool fatal = true) is cached {
    static const git_output = BuildSh.method(:git_ + operation)(BuildSh)
    if (! BuildSh.is_git_ref_name(git_output) ) {
      die "[FATAL] #{__METHOD_NAME__}: git_#{operation}: #{caller_name} requires #{Dir.cwd} to be a git repository" if (fatal)
      false
    } else {
      git_output
    }
  }

  -> is_git () is cached -> Bool {
    [:branch, :hash].all{ BuildSh.try_git(_, __METHOD_NAME__, fatal: false) }
  }

  -> platform_vm_ext (n = nil) is cached -> String {
    given ( n || BuildSh.os_name ) {
      when (:windows) { ".exe" }
      default { "" }
    }
  }

  -> platform_lib_name (n = nil) is cached -> Pair {
    static const N = (n || BuildSh.os_name)
    (
      given (N) {
        when (:windows) { "" }
        default { "lib" }
      } Â¦ given (N) {
        when (:windows) { ".dll" }
        when (:macos) { ".dylib" }
        default { ".a" }
      }
    )
  }

  -> platform_makefile (n = nil) is cached -> File {
    static const N = (n || BuildSh.os_name)
    ( N ~~ :windows ? BuildConst::N_MAKEFILE : BuildConst::GNU_MAKEFILE )
  }

  -> factor_version () is cached {
    const result = BuildSh.platform_makefile.open_r.lines.find{ _ ~~ BuildConst::VERSION_REGEX } \
      || die "[FATAL] #{__METHOD_NAME__}: can't find a VERSION in GNUmakefile #{BuildSh.platform_makefile.abs}"
    (BuildConst::VERSION_REGEX =~ result)[0]
  }

  -> feature_reproducible () is cached -> Bool {
    Bool( BuildSh.platform_makefile.open_r.lines.find{ _ ~~ BuildConst::REPRODUCIBLE_REGEX } )
  }

  # -> platform_make (*args) {
  #   const N = (n || BuildSh::os_name())
  #   ( N ~~ :windows ? ['set CL=/MP & nmake'] : ['make', '-j'] )
  # }

  -> make_clean (Bool output = false) -> Bool {
    Combi::first( Command::make(:clean, output: output) )
  }

  # bo_target: BuiltObj
  -> make_factor ( bo_target, Bool output = false ) {
    # var (pre, suf) = BuildSh::platform_lib_name()...
    (BuildSh.generate_make_targets(){:make_vm} -> cleave(
      { say "Building '#{_}' with infinite cores" },
      { Command::make(
          _,
          (BuildSh.feature_reproducible ? 'REPRODUCIBLE=1' : ' SITE_CFLAGS="-D__DATE__=\'\"[reproducible-workaround]\"\' -D__TIME__=\'\"\"\'" '),
          output: output
      ) }
    ).last \
      && :factor.cleave(
        Set(:vm, :lib).map{
            |name|
          { |fc|
            # const f = File( factor.join( bo_target.method(name+:_format)()[0, 2] ) )
            # say "file #{f} #{bo_target.method(name+:_fullname)()}"
            # const format =
            # say "#{name} format: #{format}"
            # const full =
            # say "#{name} full: #{full}"
            # ...
            const from = bo_target.method(name + :_format)().ordered_run_keys(:pfx_platform, :ext).join(fc)
            const to = bo_target.method(name + :_fullname)()
            # say "from: #{from}; to: #{to}"
            File( from ).move( to )
            # say "st: #{r}"
            # r
          }
        }...
      ).all
    )
  }

  -> make_clean_factor (/* BuiltObj */ bo_target, Bool no_clean = false, Bool output = false) {
    ( no_clean ? true : BuildSh.make_clean(output: output) ) && BuildSh.make_factor(bo_target, output: output)
  }

  # aka set_boot_image_vars
  -> boot_image_urls () is cached {
    -> curbranch_file_url (String filename, Bool use_non_master = true) is cached {
      BuildConst::BASE_REMOTE_IMAGE_URL.sprintf(
        (use_non_master ? BuildSh.try_git(:branch, __METHOD_NAME__) : :master),
        filename
      )
    }

    const non_master_branch_ok = Bool( BuildSh.make_http_request( curbranch_file_url(BuildConst::CHECKSUM_FILENAME) ) )
    Hash(
      :checksum => curbranch_file_url(BuildConst::CHECKSUM_FILENAME, use_non_master: non_master_branch_ok),
      :boot_image_url => curbranch_file_url( BuildSh.generate_make_targets(){:boot_image}, use_non_master: non_master_branch_ok )
    )
  }

  # failure case?
  -> _process_checksums_file (String data, String want_name) {
    with ( data.lines.find{ .contains(want_name) } ) {
      .split(" ").second
    } else {
      die "#{__METHOD_NAME__}: !!! gibberish checksum data when looking up '#{want_name}':\n#{data}"
    }
  }

  -> _get_boot_image {
    const File boot_image_file = BuildSh.generate_make_targets(){:boot_image}
    say (__METHOD_NAME__ + ': Downloading boot image file ' + boot_image_file)

    const boot_image_urls = BuildSh.boot_image_urls

    const boot_image_data = BuildSh.make_http_request( boot_image_urls{:boot_image_url} )
    const local_md5 = boot_image_data.md5

    const remote_checksum_data = BuildSh.make_http_request( boot_image_urls{:checksum} )
    if (! remote_checksum_data) {
      say "#{__METHOD_NAME__}: Can't download, can't checksum"
      return false
    }
    const remote_md5 = BuildSh._process_checksums_file(remote_checksum_data, boot_image_file)

    say "#{__METHOD_NAME__}: boot image sums: #{remote_md5} / #{local_md5}"
    if (remote_md5 != local_md5) { die "#{__METHOD_NAME__}: !!! downloaded checksum mismatch !!! (unrecoverable)" }

    boot_image_file.touch.cleave(
      { .binmode },
      { .spurt( boot_image_data ) }
    )
  }

  # TODO: allow the checksum of local boot image to fail due to no internet, but not break the build
  # TODO: allow the checksum of local boot image to be stored locally
  -> update_boot_image {
    const File boot_image_file = BuildSh.generate_make_targets(){:boot_image}
    say "#{__METHOD_NAME__}: Removing old data..."
    File(BuildConst::CHECKSUM_FILENAME).remove
    # remove all boot images with a name like BOOT_IMAGE.{?,??}
    # File(boot_image_name + '*').glob.grep{ _ ~~ Regex("#{boot_image_name}\.(?:.|..)") }.map{ File(_).remove }
    File("temp/staging.*.image").glob.each{ File(_).remove }

    say "#{__METHOD_NAME__}: Processing #{boot_image_file}"

    if ( boot_image_file.exists ) {
      say "#{__METHOD_NAME__}: have a local #{boot_image_file}, need to checksum"
      const boot_image_urls = BuildSh.boot_image_urls
      const remote_checksum_data = BuildSh.make_http_request( boot_image_urls{:checksum} )
      if (! remote_checksum_data) {
        say "#{__METHOD_NAME__}: Can't download, can't checksum"
        return false
      }
      const remote_md5 = BuildSh._process_checksums_file(remote_checksum_data, boot_image_file)

      const local_md5 = boot_image_file.md5
      say "#{__METHOD_NAME__}: boot image sums: #{remote_md5} / #{local_md5}"

      if (remote_md5 != local_md5) {
        say "\t#{__METHOD_NAME__}: !!! local / remote checksum mismatch !!! "
        boot_image_file.remove
        return BuildSh._get_boot_image
      }
      say "#{__METHOD_NAME__}: Local boot image matched remote sum"
      true
    } else {
      say "#{__METHOD_NAME__}: no local #{boot_image_file}, need to get"
      BuildSh._get_boot_image
    }
  }

  -> blob_file_contents_sum (File f, Str algo = :sha256) { f |> :open_r |> :slurp |> :encode_utf8 |> (algo || :sha256) }
}
