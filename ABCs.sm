#! ruby

# Hello Mother!
class Object {
  # Apply a series of blocks to an object, accumulating the results in an Array
  # also useful to bind a value to a name or _ without an explicit definition
  # sort of like `let` in lisp
  method cleave (*blocks) {
    blocks.map{ .run(self) } # ...
  }
  # Apply the block CARs in the argument to self, using the
  #   CDRs as the blocks' arguments, and accumulating the results in an array
  method ifold (*bv_pairs) {
    bv_pairs.map{ |p| p.first.run( self, @|(p.second) ) }
  }
  # ???
  -> _map_cleave_r (blocks, vals) {
    if (blocks) {
      # clone?
      var newval = blocks[0].run(self)
      self = newval
      self._map_cleave_r( [ blocks[1..blocks.len] ], [vals..., newval] )
    }
  }
  # by-block accumulate
  method map_cleave (*blocks) {
    var vals = []
    foreach blocks {
        |block|
      vals.append( block.run(self) )
      self = vals.last
    }
    vals
  }

  # Needs research: how do the following relate to / overlap with / are obviated by
  #   Object.respond_to?

  # Simply whether the object says it declares the method `name`
  #   without attempting to call that method
  # Sometimes, especially for builtin types, a method appears in `.methods`
  #   but cannot actually be called

  # Such methods can be used to tell whether an object is a bare typename
  # But also very useful to ask whether the method is publically declared
  #   without calling it
  # This only searches public methods, and those not starting with an underscore _
  -> declares_method (String name) -> Hash {
    self.methods.grep_kv{ |n, _| n ~~ name }
  }

  # Builtin Object method: test only whether the object has a method by that
  #   name, regardless of dispatch on arguments
  /* -> respond_to (String name, *_) -> Bool { self.can(name, _...) } */

  # -> Bool or, if give_msg is true, the text of the error messsage
  # Check whether the object can respond to the method with the given
  #   number of arguments, without the possibility of changing the object
  #   itself

  # True if the method call with the number of arguments didn't throw an exception
  #   false otherwise, or the error message
  -> has_overload (String name, Bool give_msg = false, *args) {
    try {
      # don't change self
      self.dclone.method(name)(args...)
      true
    } catch {
        |_, msg|
      give_msg && msg
    }
  }

  # The object has declared the method by the name, and it is invokable
  #   though it may fail overload resolution with the specific number of arguments
  # If the invocation throws "no method" for example, it's not even invokable

  # The method is public if it appears in `.methods`
  # The method is concrete if it can be successfully invoked, or if it can
  #   be invoked with the wrong number of arguments
  -> has_public_concrete_method (String name, *args) -> Bool {
    self.cleave(
      { .declares_method(name) },
      {
        const res = .has_overload(name, give_msg: true, args...)
        (res ~~ /does not match/) || (res == true)
      }
    ).all
  }

  # The object has declared the overload of the method by the name with the
  #   specific number of arguments

  # The overload is public if its parent LazyMethod name appears in `.methods`
  # The overload is concrete if it can be successfully invoked with the specific
  #   number of arguments
  -> has_public_concrete_overload (String name, *args) -> Bool {
    self.cleave( { .declares_method(name) }, { .has_overload(name, args...) } ).all
  }
  # there's no direct way to tell if an object method is a property declared with "has"
  #   or an accessor declared with -> name ...
  # but if we call it with too many / wrong type arguments and it fails, it's a setter accessor
  #   if it's called with too many / wrong type arguments and doesn't care, it's a property
  # this does require accessors to have a clearly defined argument list, hopefully with types
  #   or else it will overwrite the accessor with a simple property...
  #   which is what you get for using simple blocks with |_| captures,
  #   or no typed parameters in setter accessors :)
  -> is_property (String name, Object arg) {
    # if has_overload fails for a reason related to invocation,
    #   it's a setter accessor with a parameter list and not a property

    # assuming the method by that name exists....

    # NOTE: we are intentionally trying to give too many arguments to an
    #   accessor-or-property method
    # if it is a declared accessor it will complain about "does not match"
    #   therefore it's not a property
    # property "methods" don't care how many arguments they get
    /does not match/ !~ self.has_overload(name, give_msg: true, arg, :extra_arg)
  }

  # Well, Aren't I special?
  -> serializable {
    self.has_overload(:serial_properties)
  }

  # find methods matching the block
  -> grep_methods (Block g) -> Hash {
    self.methods.grep_kv(g)
  }
  # like grep_methods, but also apply a block to the results
  -> grep_map_methods (Block g, Block m) -> Hash {
    self.grep_methods(g).map_kv(m)
  }

}

class Array {
  method all_eq {
    self«==»
  }
  method all {
    self.all{ _ }
  }
  method none {
    self.all{ !_ }
  }
  method all_neq {
    self.combinations(2).map{ !.all_eq }.all
  }
}

# Extract the base fully-qualified typename from the "overqualified" Perlescent
#   stringified typename
func transform_typename (String typename) -> String {
  (/^Sidef::Runtime::\d+::(main::.+)$/ =~ typename)[0]
}

# Try to figure out if the object is a raw typename
#   If your class defines these specific properties too,
# Sidef and I will probably scream
func is_baseclass (Object obj) -> Bool {
  [:compile_to_perl, :code_interactive].all{ obj.declares_method(_) }
}
func is_typename  (Object obj) -> Bool { is_baseclass(obj) }

# Whether the object is a fully-qualified constructed object
# i.e. an invoked object like String() or MyClass()
func is_fqco (Object obj, extra_property = false) -> Bool {
  !is_typename(obj) && (
    extra_property
      ? Bool( obj.declares_method(extra_property) )
      : true
  )
}


# i really must apologise for writing all these abstract classes, it's unlike me
# something has come over me... it's simply too easy.....


# a Sidef object can be turned into a pure-perl representation for the purpose of
#   use in JSON etc, and then turned back to Sidef again
/* abstract interface */ # /
class Serializable {
  has String _k_stype = :_serial_type
  has String _k_svalue = :_serial_value
  # has Hash serial_properties

  -> _impl_serial_properties_excluding (Array x) -> Hash {
    self.grep_methods{ |name, _| name ~~ /^serial_properties/ } \
      .values \
      .map{
          |calls|
        calls().grep_kv{
            |prop_name, _|
          !x.contains(prop_name)
        }
      } \
      .reduce{ |l, r| l.merge(r) } \\ Hash()
  }
  # -> _do_serial (Array x) -> Block {
  #   func (Block d) { self.serial_properties_excluding(x).map_kv(d) }
  # }
  -> _do_serial (Array x, Block d) -> Hash {
    self._impl_serial_properties_excluding(x).map_kv(d)
  }

  # This will magically disappear as soon as subsets can be ~~ smartmatched against
  -> _impl_is_serialized_hash (Object obj) -> Bool {
    obj.is_a( Hash ) && obj.keys.sort==[self._k_stype, self._k_svalue] && obj{self._k_svalue}.is_a( Hash )
  }

  # every object we encounter is either Serializable or builtin
  # if it's incorrectly written as non-builtin and non-Serializable
  # then it will appear as a round-trip error
  method serialize (Array exclude = [], Bool only_extant_fields = false) -> Hash {
    self._do_serial(exclude, {
        |prop, typename|
      # the prop name could be a raw property, an accessor, or nonexistent entirely
      # if it doesn't exist, only die when only_extant_fields is true,
      #   else pass with nil

      # call the accessor if it's an accessor, otherwise call the property-method
      # if the method isn't callable for some reason, try directly accessing the property
      #   which probably won't yield a different result and still die

      const value = if ( self.has_overload(prop) ) {
        self.method( prop )()
      } elsif ( self.is_property(prop, typename()) ) {
        self{prop} \\ (!only_extant_fields
          ? nil
          : die (
            "#{__METHOD_NAME__}: Field '#{prop}' is a nil field without an ",
            "accessor, or does not exists entirely! ",
            "#{__CLASS_NAME__}::#{__METHOD_NAME__}(only_extant_fields: true, ...)")
          )
      }

      # say "serialize '#{prop}' serializable: #{s10e} type: #{typename} value: #{value.dump}"
      ( (prop) => (
        value.serializable
          ? Hash(
            (self._k_stype) => typename,
            (self._k_svalue) => value.serialize(exclude: exclude, only_extant_fields: only_extant_fields))
          : value
        )
      )
    })
  }

  # NOTE: no longer using eval because we have enough type information
  #   might use it as a backup in the future though
  # the justification for the scoping rules that introduce eval_scope
  #   is entirely legitimate
  # what is not entirely legitimate is the need to use eval to roundtrip
  #   typenames in the first place
  # can we have some kind of python-like globals() and locals() calls for eval?
  method unserialize (Hash data, Array exclude = [], Bool only_extant_fields = false) -> __CLASS__ {
    self._do_serial(exclude, {
        |prop, typename|
      # when only_extant_fields is true, don't fill all missing keys in data as nil
      if (only_extant_fields && !data.has_key(prop) ) {
        return nil
      }
      const entry = data{prop}
      # say "unserialize call method '#{prop}'"

      # making a new SerialHash container class would defeat the purpose of Serializable
      # we need to distinguish "internal" hashes from actual ones
      # missing feature: can't smartmatch against subsets

      if ( !self._impl_is_serialized_hash(entry) ) {
        entry
      } else {
        # we need to check if serial_type is a second-class stringified typename
        # say "serial_type: #{serial_type.ref} #{is_baseclass(serial_type)}"
        given ( entry{self._k_stype} ) {
          case ( (
              .is_a( String ) \
                && is_fqco(_, extra_property: :len) \
                && (transform_typename(_) == transform_typename(typename.ref))
          ) ) {
            # serial_type is a String() object, not the String base class
            typename
          }
          case ( .is_a( typename ) && is_typename( _ ) ) {
            # serial_type is a base class
            _
          }
          default {
            # serial_type is either the wrong typename for the property
            #   or a constructed object, not a typename
            die "unserialize typecheck: '#{_.dump}' !~ '#{typename.dump}'"
          }
        }().unserialize(
          entry{self._k_svalue},
          exclude: exclude,
          only_extant_fields: only_extant_fields
        )
      }.cleave{
          |derived|
        # need to test whether prop is an accessor or plain property-method
        if ( self.is_property(prop, derived) ) {
          # say "#{prop} is a property"
          self{prop} = derived
        } else {
          # say "#{prop} is an accessor"
          self.method( prop )( derived )
        }
      }
    })
    self
  }
}

# use the serializable functionality to equate two objects
/* abstract interface */
class SeriallyEquatable < Serializable {
  -> _serial_eq (other, exclude_props = []) -> Bool {

    if (other ~~ Serializable) {
      other.serialize(exclude: exclude_props) == self.serialize(exclude: exclude_props)

    # somehow... maybe other is our superclass
    } elsif (other ~~ self) {
     [self, other].map{
       |obj| self._do_serial(exclude_props, { obj.method(_)() } )
     }«==»

    } else { self._do_serial(exclude_props, { other{_} == self{_} } ).all }
  }
  method == (other) { self._serial_eq(other) }
}

# finally some concrete code
class DirSum (Str algo = '', Str names = '', Str contents = '') < SeriallyEquatable {
  has Hash serial_properties = [:algo, :names, :contents].map{ ( (_) => Str ) }.to_h
}

class SrcSums (DirSum vm = DirSum(), DirSum core = DirSum(), DirSum basis = DirSum(), Bool basis_ignored = false ) < SeriallyEquatable {
  has Hash serial_properties = Hash(
    :basis_ignored => Bool,
    [:vm, :core, :basis].map{ ( (_) => DirSum ) }...
  )
}

class BlobSums (Str algo = '', Str vm = '', Str lib = '', Str boot = '') < SeriallyEquatable {
  has Hash serial_properties = [:algo, :vm, :lib, :boot].map{ ( (_) => Str ) }.to_h
  method =~ (other) { self._serial_eq(other, exclude_props: [:boot]) }
}

class VirtualBase64Blob (Str data = '', Str algo = '', Str sum ='') < SeriallyEquatable {
  has Hash serial_properties = [:data, :algo, :sum].map{ ( (_) => Str ) }.to_h
}

# call side effecting blocks in methods of obj found by the regex r
-> _call_se_blocks_in_methods_by (Object obj, Regex r) {
  obj.grep_map_methods(
    { |name, _| name ~~ r },
    { |_, calls| calls().each{ |block| block.run(obj) } }
  )
}


# generates accessors based on the serializable properties
/* abstract interface */ # /
class SeriallyAccessible < SeriallyEquatable {
  has Bool _impl_locked = false
  has String _impl_write_locked = 'attempt of write to locked '+__CLASS_NAME__+' object!'

  -> lock { self._impl_locked = true }
  -> lock (Bool b) { self._impl_locked = b }
  -> unlock { self._impl_locked = false }

  -> _bind_self_in_accessor_generator () -> Block {
    {
        |k, v|
      -> try_index(Num i, Block b = { _ }) { (is_fqco(v) && v.is_a(Array)) ? v[i] : b.run(v) }
      const typename = try_index(0)
      const default_val = try_index(1, { _() } )
      const custom_blocks = try_index(2, { Hash() } )

      (typename ~~ default_val) \
        || die (
          "SeriallyAccessible > #{__CLASS_NAME__}: init: type mismatch in default value for '#{k}': ",
          "want '#{typename}'; got '#{default_val.class}' ('#{default_val.dump}')"
      )

      # create the slot
      self{:_ + k} = default_val

      const Block setter_prelude = {
        .is_a(typename) \
          || die(
            "SeriallyAccessible > #{__CLASS_NAME__}: #{__METHOD_NAME__}: ",
            "type mismatch in setter '#{k}': want '#{typename}'; got '#{_.class}'  ('#{default_val.dump}')"
        )

        die (self._impl_write_locked, ": in ", __METHOD_NAME__) if (self._impl_locked)
      }

      # following requires Sidef parser patch

      # # conditionally define a function in the current scope...
      # ( defined( custom_blocks{:get} )
      #   ? func accessor (self) { custom_blocks{:get}.run(self) }
      #   : func accessor (self) { self{:_ + k} }
      # )
      # # ...with an overload
      # ( defined( custom_blocks{:set} )
      #   ? func accessor (self, new_val) { setter_prelude.run(new_val); custom_blocks{:set}.run(self, new_val) }
      #   : func accessor (self, new_val) { setter_prelude.run(new_val); self{:_ + k} = new_val }
      # )
      func accessor (self) {
        static var custom_getter = custom_blocks{:get}
        if (custom_getter) { custom_getter.run(self) } else { self{:_ + k} }
      }
      func accessor (self, new_val) {
        static var custom_setter = custom_blocks{:set}
        setter_prelude.run(new_val)
        if (custom_setter) { custom_setter.run(self, new_val) } else { self{:_ + k} = new_val }
      }
      __CLASS__.def_method(k, accessor)
    }
  }

  method init {
    say "#{__CLASS_NAME__}: #{__METHOD_NAME__}"
    _call_se_blocks_in_methods_by(self, /^extra_init_steps/)

  }
}

class TimeSpec < SeriallyAccessible {
  has Array extra_init_steps_base = [
    { ._do_serial([], ._bind_self_in_accessor_generator) },
    { .grep_map_methods(
     { _ ~~ /^serial_property_generator/ }, # => Hash
     ._bind_self_in_accessor_generator
    ) },
  ]

  has Hash serial_properties = [:created, :modified, :accessed, :tombstoned].map { ( (_) => Num ) }.to_h
}

class BuiltObj < SeriallyAccessible {
  has Array extra_init_steps_base = [
    { ._do_serial([], ._bind_self_in_accessor_generator) },
    { .grep_map_methods(
       { _ ~~ /^serial_property_generator/ }, # => Hash
       # try a bigger gun (different method!)
       { |k, v| v().map{ .run(self) } }
     ).map_kv( ._bind_self_in_accessor_generator ) },
  ]

  has Array _blob_targets = [:vm, :lib, :image, :boot]
  has Hash serial_properties = Hash(
    :id => Str,
    :factor_version => Str,
    :is_git => Bool,
    :git_branch => Str,
    :git_hash => Str,

    :os_type => Str,
    :os_name => Str,
    :arch_name => Str,
    :word_size => Str,

    :stored_boot_image => VirtualBase64Blob,
    :src_sums => SrcSums,
    :blob_sums => BlobSums,
    :blob_algorithm => [Str, :sha256, :( :get => { .blob_sums.algo || ._blob_algorithm } ) ], # default: sha256

    :timespec => TimeSpec,
  )

  # going to generate accessors instead of naming them
  # note the lack of def_method in the block; this simply arbitrarily
  #   extends the serial_properties hash to make new accessors
  #
  # all same length and support platform-prefixes
  # vm_format, etc
  has Array serial_property_generator_builtobj = [ {
    var c = ._blob_targets.map{
      ( (_ + :_format) => Array)
    }.to_h
    say c
    c
  } ]

  # these are not accessors, just methods that glue accessed values together
  # create boot_fullname etc
  has Array extra_init_steps_builtobj = [ {
      |self|
    self._blob_targets.each{
        |name|
      const name_format = name+:_format
      func gen (self) -> File {
        File( String( self.method(name_format)[0, 1] ) + self.id + self.method(name_format)[2] )
      }
      __CLASS__.def_method(name + :_fullname, gen)
    }
  } ]

  # next step: code generation for these methods too, by a special _extra_init_blocks
  #   property that get invoked at the end of init() to generate these methods
  # want a version of insert that doesn't change its object unless ! is used
  # -> vm_fullname ()    -> File { File( "#{self.vm_format[0, 1]}#{self.id}#{self.vm_format[2]}" ) }
  # -> image_fullname () -> File { File( "#{self.image_format[0, 1]}#{self.id}#{self.image_format[2]}" ) }
  # -> lib_fullname ()   -> File { File( "#{self.lib_format[0, 1]}#{self.id}#{self.lib_format[2]}" ) }
  # -> boot_fullname ()  -> File { File( "#{self.boot_format[0, 1]}#{self.id}#{self.boot_format[2]}" ) }
}


/* abstract */ # /
class WithDirectory {
  -> with_directory (operation) {
    var oldwd = Dir.cwd
    if (oldwd == self.wd) {
      operation.run
    } else {
      self.wd.chdir
      var r = operation.run
      oldwd.chdir
      r
    }
  }
}

/* abstract */ class CapturedWriter < WithDirectory {
  method capture_write_disk (String caller_name, Block operation) {
    if (! self.dry_run) {
      self.log.vbs(__FILE__, __LINE__, __METHOD_NAME__, "#{caller_name}: writing disk with", operation)
      return self.with_directory( operation )
    }
    self.log.vbs(__FILE__, __LINE__, __METHOD_NAME__, "#{caller_name}: not writing disk (dry run)")
    nil
  }
}
