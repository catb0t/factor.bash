#! ruby
#! sidef -vWkP8
#`(exit the shell) {
  eval 'exec sidef -WkP8 $0 ${1+"$@"}'
}
##  MultiFactor, meta build system for Factor core development
##    Copyright Cat Stevens 2018
##  This program is free software: you can redistribute it and/or modify
##    it under the terms of the GNU General Public License as published by
##    the Free Software Foundation, either version 3 of the License, or
##    (at your option) any later version.
##
##    This program is distributed in the hope that it will be useful,
##    but WITHOUT ANY WARRANTY; without even the implied warranty of
##    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
##    GNU General Public License for more details.
##
##    You should have received a copy of the GNU General Public License
##    along with this program.  If not, see <https://www.gnu.org/licenses/>.

include sidefext::metatype

include lib::ABCs
include lib::msg_classes
include lib::feature
include lib::withdirectory

include providers::argparse
include providers::builtobj
include providers::cachedb
include providers::factorbash

define Hash SCRIPT = :(
  "version" => "0.3", # "
  "name" => "multifactor",
  "doc" => "a meta build system for Factor core development",
  "bug" => "github.com/catb0t/multifactor/issues",
  "license" => "GPL v3+"
)

# basic variable definitions for working with Factor from a high-level perspective
module Factor {

  define File RC_FILE = Dir.home+%f(.factor-rc)

  define Hash _config_lines_cache = []

  define Block rc_config_lines = func () -> Hash {
    Factor::_config_lines_cache[0] \\= Factor::RC_FILE.open_r.map{
      [ (.=~ /^! multifactor:(.+) ?(.*)/)[0, 1] ]
    }.grep.map{ _... }.to_h
  }

  define Block _config_cache = :()

  define Block find_config = func find_config (name, Bool must_exist = true) {
    Factor::_config_cache{name} \\= (Factor::rc_config_lines(){name} \\ do {
      must_exist && die "Required config '! multifactor:#{name}' not found in #{Factor::RC_FILE}"
      ''
    })
  }

  define Maybe::Maybe FACTOR_DIR = Maybe::MaybeDir(  Factor::find_config('factor-dir', false) )
  define Maybe::Maybe QUIET_PIPE = Maybe::MaybeBool( Factor::find_config('quiet-pipe', false) )
  define Maybe::Maybe DEFAULT_CHECKSUM = Maybe::MaybeStr(
    Factor::find_config('default-checksum', false) || Feature::DEFAULT_CHECKSUM
  )

  # ???
  define Block say_stderr = func (String color, *args) {
    if (STDOUT.is_on_tty || (!STDOUT.is_on_tty && !Maybe::unmaybe(Factor::QUIET_PIPE, { false }) ) ) {
      STDERR.autoflush(true)

      with ( Feature::ANSIColor ) {
        STDERR.say( .color('bold', color), args..., .RESET )
      }
    }
    # else, not a TTY and quiet-pipe is true
  }

  define Hash  TARGETS = :( :vm => true, :lib => true, :image => false )
  define Array CHECKSUMMED_TARGETS = TARGETS.grep_kv{ |_k, _| _ }.keys.sort
}

module MultiFactor {

  include sidefext::iterable
  import sidefext::iterable::ImmutableHash

  include lib::shell_words

  include Factor
  include FactorBashStateless
  include GuardIO
  import lib::shell_words::BuildSh

  include lib::breakpoint
  include Breakpoint
  import Breakpoint::bp

  import builtobj::DirSum
  import builtobj::SrcSums
  import builtobj::BlobSums
  import builtobj::VirtualBase64Blob
  import builtobj::TimeSpec
  import builtobj::BuiltObj

  # historical interest
  # func compare_properties (Array properties, BuiltObj b, Object o, Bool debug = false) {
  #   if (debug) {
  #     properties.map{
  #         |property|
  #       const db_property = b.method(property)()
  #       const status_property = o{property}
  #       say "Property '#{property}': #{db_property == status_property}: #{db_property.dump} == #{status_property.dump}"
  #       db_property == status_property
  #     }.all
  #   } else {
  #     properties.map{ |prop| b.method(prop)() } ~Z== o{properties...} -> all
  #   }
  # }
  struct BlobStatus {
    Bool exists = false
    Bool recompiled = false
    Maybe::Maybe_Type checksum = Maybe::MaybeStr(nil)
  }

  func dump (BlobStatus b) {
    with (b) {
      "BlobStatus( exists: #{.exists}, recompiled: #{.recompiled}, checksum: #{Maybe::dump(.checksum)} )"
    }
  }

  struct AlgoBox {
    String blob
    String src
  }

  func absent_config (String rc_name, param_name = nil) {
    die "Absent config!\n\t'#{rc_name}' or '#{param_name \\ rc_name}' must be set in either the RC file ('! multifactor:#{rc_name} ...') or command line argument ('--#{param_name \\ rc_name}=...')!"
  }

  define Set _mfr_known_actions = Set('run-factor', 'env-info', 'src-sums', 'spoof-exists')

  define Array _mfr_sprop_gen = [ {
    .field_groups.map{
      const tn =
      .second.map{ |k| ((k) => .first) }.to_h
    }.reduce('merge')
  } ]

  # these are all command-line options except uuid
  define Array _mfr_fg = [
    (Bool    ¦ [:force, :debug, :info, :dry_run, :spoof_build, :clean, :basis_dev, :commit_db]),
    (String  ¦ [:action, :uuid, :sum_algo]),
    (Dir     ¦ [:wd]),
  ]

  define Array _mfr_init = [ {
      |self|
    self.uuid( GuardIO::make_uuid() )
    say "MFRuntime: #{self.uuid}"
    self.wd( Dir.cwd )
    # need to use field_groups here
    Hash(
      :action => Block.identity, # identity constructor
      :sum_algo => String
    ).merge(
      [:force, :dry_run, :info, :clean, :basis_dev, :spoof_build] \
        .map{ ( (_) => Bool ) }.to_h # all these parentheses are important
    ).each_kv{
        |name, convert|
      self.method( name )( convert( self.options(){ name.replace_all("_", "-") } ) )
    }

    with (self.sum_algo || Maybe::unmaybe(Factor::DEFAULT_CHECKSUM, { absent_config('default-checksum', :sum_algo) }) ) {
      self.algos = AlgoBox( [ _ ]*2 ... )
    }

    const Dir factor_dir = Dir( with ( 'factor-dir' ) {
        self.options(){_} || Maybe::unmaybe(Factor::FACTOR_DIR, { absent_config(_) } )
    } )

    const Bool is_trace = ( [:trace, :verbose, 'dry-run'].any{ self.options(){_} } )
    const Bool is_verbose = ( [:verbose, 'dry-run'].any{ self.options(){_} } )
    const Bool subcommand_output = Bool( with ( self.options ) {
      .{'no-quiet-subcom'} || ( .{'quiet-subcom'} \\ Maybe::unmaybe(
        Factor::QUIET_PIPE,
        { absent_config('quiet-pipe', '[no-]quiet-subcom') }
      ) )
    } )

    # need a way for the consumer ofthe ArgParse API to know the exact syntax of the command line
    self.known_actions.has(self.action) \
      || die "[FATAL] #{__FILE__, __LINE__}: init_steps_mfruntime: don\'t know action '#{self.action}' [--action | -a]"

    self.commit_db( Set('run-factor', 'spoof-exists').has(self.action) )

    # initialise helper objects
    self.log = GuardIO::SLog( is_trace: is_trace, is_verbose: is_verbose, logger: Factor::say_stderr )

    self.fbash = providers::factorbash::FactorBash(
      factor_dir,
      self.algos.src,
      self.basis_dev,
      subcommand_output: subcommand_output,
      spoof_build: self.spoof_build
    )

    const File db_name = File(self.options(){'db-name'})

    self.locker = GuardIO::GlobLocker().setup(
      :uuid => self.uuid,
      :remove_old_lock => self.options(){'remove-old-lock'},
      :lock_ext => '.multifactor_lock',
      :locked_fods => [ db_name, self.fbash.wd ],
      :verbose => is_verbose,
      :trace => is_trace
    )

    const Set promised_locks = self.locker.do_lock_files

    self.db_man = providers::cachedb::CacheDB().setup(
        :dir =>              self.options(){'db-dir'},
        :filename_base =>    db_name,
        :dry_run =>          self.dry_run,
        :verbose =>          is_verbose,
        :trace =>            is_trace,
        :replace_existing => self.options(){'from-scratch'},
        :promised_locks =>   promised_locks
        # :element_type =>     BuiltObj,
        # :logger => self.log # separate or same loggers?
    )

    self.log.trc(__FILE__, __LINE__, :init_steps_10_mfruntime, "finished configuration") #, wd now: '#{self.db_dir}'")
  } ]

  class MFRuntime (Hash options, Array factor_arguments) << lib::ABCs::SeriallyAccessible, lib::withdirectory::CapturedWriter {

    has GuardIO::SLog                      log
    has providers::cachedb::CacheDB        db_man
    has GuardIO::GlobLocker                locker
    has providers::factorbash::FactorBash  fbash
    has AlgoBox                            algos

    # todo: collect generated methods that don't appear in .methods

    has Array known_actions = _mfr_known_actions
    has Array field_groups = _mfr_fg
    has Array serial_property_generator_mfruntime = _mfr_sprop_gen
    has Array init_steps_10_mfruntime = _mfr_init

    method destroy (Bool is_exc, Hash exc_info, *rest) {
      say "~MFRuntime entry"
      const type = (is_exc && Combi::rescope( lib::msg_classes::classify( exc_info{:msg} ) )({
          |type, fields|
        self.log.wrn(__FILE__, __LINE__, __METHOD_NAME__, "#{__CLASS_NAME__}: destructor called!\n\t#{type} :: #{fields}\n") # .grep_kv{ .!= :block_tried_invoke }
        type
      }))
      [ [self.db_man, write: self.commit_db], [self.locker]].map{ .[0].destroy(is_exc, exc_info, @|.slice(1) ) }
      type
    }

    method show_info { ... }

    -> _blob_file_contents_sum (File f, Str algo) -> String {
      # self.log.vbs(__FILE__, __LINE__, __METHOD_NAME__, "checksumming #{f} with algorithm '#{algo}'")
      # give checksum of empty file, or die?
      self.spoof_build ? '(empty checksum)' : self.fbash.with_directory{
        (f.exists && BuildSh.blob_file_contents_sum(f, algo || self.algos.blob)) \
          || die "[FATAL] #{__METHOD_NAME__}: empty algo: file: #{f}"
      }
    }

    -> compile_vm_lib (BuiltObj bo, Bool clean_would_race) -> Bool {
      if ( ! self.fbash.build_factor(bo, clean_would_race) ) {
        die "vm compilation failed!"
      }
      self.log.vbs(__FILE__, __LINE__, __METHOD_NAME__, "vm and/or lib compiled")
      true
    }
    # -> compile_vm  (o, c) { self.compile_vm_lib(o, caller_name: c) }
    # -> compile_lib (o, c) { self.compile_vm_lib(o, caller_name: c) }

    # TODO:REWRITE
    -> compile_image (BuiltObj bo) -> Bool {
      if (! self.fbash.build_image(bo) ) {
        die "image compilation failed!"
      }
      self.log.vbs(__FILE__, __LINE__, __METHOD_NAME__, "image compiled")
      true
    }

    # TODO:REWRITE
    # NOTE: modifies obj
    -> sum_save_boot_image (BuiltObj bo) {
      self.log.trc(__FILE__, __LINE__, __METHOD_NAME__, "saving boot image sums for #{bo.boot_fullname}")
      const disk_sum_toplevel = self._blob_file_contents_sum(bo.boot_fullname, bo.blob_algorithm)

      if ( disk_sum_toplevel != bo.blob_sums.boot ) {
        bo.blob_sums( BlobSums(
          algo: (bo.blob_algorithm || self.algos.blob),
          boot: disk_sum_toplevel,
          # set the rest of the properties to their old values
          Factor::CHECKSUMMED_TARGETS.map{
              |n|
            n ⫶ bo.blob_sums.method(n)()
          }...
        ))
      }

      # const disk_sum_stored = self._blob_file_contents_sum(bo.boot_fullname, bo.stored_boot_image.algo)
      # if ( disk_sum_stored != bo.stored_boot_image.sum ) {
      #   bo.stored_boot_image( VirtualBase64Blob(
      #     sum: disk_sum_stored,
      #     data: self.fbash.with_directory{ GuardIO::read_binary_b64(bo.boot_fullname) },
      #     algo: (bo.stored_boot_image.algo || bo.blob_algorithm || self.algos.blob)
      #   ) )
      # }
    }

    # TODO:REWRITE
    # complex check for vm and lib
    # -> maybe_recompile_target (BuiltObj match_bo, BlobSums cur_blob_sums, String target, Hash previous_recompiled) { }

    -> _algo (BuiltObj prefer) is cached -> String { prefer.blob_algorithm || self.algos.blob }

    # TODO:REWRITE
    -> _make_disk_blob_sums (BuiltObj bo) -> BlobSums {
      BlobSums(
        # TODO: algo
        algo: self._algo(bo),
        self.fbash.with_directory{
          Factor::CHECKSUMMED_TARGETS+[:boot].map{
            ( _ ⫶ self._blob_file_contents_sum( bo.method(. + :_fullname)(), self._algo(bo) ) )
          }
        }...
      )
    }

    -> build_new_factor (BuiltObj current_status) {
      const MN = __METHOD_NAME__

      self.log.trc(__FILE__, __LINE__, MN, 'no match in db!')
      # interpret the current_status keys as property (setter) names in BuiltObj
      # this call does not set all properties, only those listed in status_properties
      current_status.dclone.unlock.id(self.uuid).timespec( TimeSpec().created(Time.now) ).cleave(
        { |n| self.compile_vm_lib(n, false) },
        { |n| self.compile_image(n)  },
        { |n| n.blob_sums( self._make_disk_blob_sums(n) ) },
        { |n| self.sum_save_boot_image(n) }, # modifies n
        { .lock },
        { self.log.trc(__FILE__, __LINE__, MN, 'writing the new BuiltObj to disk db because it changed') },
        { self.db_man.insert(_) },
        { self.log.trc(__FILE__, __LINE__, MN, "ok, running factor!") },
        { self._detach_factor_thread(_.vm_fullname) }
      )
    }


    -> _tombstone_entry (BuiltObj bo) {
      const tombstoning = (bo |> :dclone |> :unlock)
      del bo
      tombstoning.timespec.tombstoned(Time.now)

      self.db_man.insert(tombstoning)
      self.capture_write_disk(__METHOD_NAME__, { Factor::TARGETS.keys.each{ tombstoning.method(_ + :_fullname)().remove } } )
    }

    -> update_existing_factor (BuiltObj bo, BuiltObj current_status, Bool is_restart: true) {
      const MN = __METHOD_NAME__

      func checksum (String tgt, LazyMethod tgt_fullname) -> Maybe::Maybe_Type {
        self._blob_file_contents_sum( tgt_fullname(), self._algo(bo) ) \
          .rescope{
              |disk_sum|
            # say "current disk sum: #{disk_sum} bo disk sum: #{bo.blob_sums.method(tgt)()}"
            Maybe::MaybeStr( bo.rescope{
              self.spoof_build || Combi::binop_pairs(
                .blob_algorithm          self._algo(bo)
                .blob_sums.method(tgt)() disk_sum
              )('==') # '
            } ? disk_sum : nil )
          }
      }
      func new_vm_lib () -> Block {
        func _new_vm_lib (Bool clean_would_race) -> (BlobStatus, BlobStatus) {
          # say "#{__FUNC_NAME__}: clean_would_race is #{clean_would_race}"
          self.compile_vm_lib(bo, clean_would_race).rescope{
              |s|
            Factor::CHECKSUMMED_TARGETS.map{ BlobStatus(
                exists: s, recompiled: s,
                checksum: checksum( _, bo.method(. + :_fullname) )
            ) }...
          }
        }
      }

      # if either the vm or lib are missing or out of date, recompile them
      #   if the checksums don't match after both have been recompiled, stop
      const Hash blob_stats = self.fbash.with_directory{
        Factor::TARGETS.map_kv{
            |k, v|
          const LazyMethod target_fullname = bo.method( k + :_fullname )
          # say "target_fullname class: #{target_fullname.class}"
          (target_fullname.exists || self.spoof_build).rescope{
            # say "making blob_stats; #{target_fullname()} exists? or spoof #{_}"
            ( (k) => BlobStatus(
              exists:     _,
              recompiled: false,
              checksum:   Maybe::MaybeStr( (v && _) ? checksum( k, target_fullname ) : nil )
            ) )
          }
        }
      }

      func need_recompile ( Hash _, String k ) -> Bool {
        const bo_checksum   = bo.blob_sums.method(k)()
        const disk_checksum = Maybe::unmaybe( .{k}.checksum )
        const matching = (bo_checksum == disk_checksum)
        # say "#{__FUNC_NAME__}: #{k}: checksums: #{bo_checksum} / #{disk_checksum}; #{bo.blob_sums.serialize.dump} ;; #{dump(.{k})}"
        !(.{k}.exists && matching)
      }
      const recompile_vm = need_recompile( blob_stats, :vm )
      # bp(__FILE__, __LINE__, __NAMESPACE__, self.class, __METHOD_NAME__, recompile_vm => recompile_vm)

      if (recompile_vm) {
        blob_stats{Factor::CHECKSUMMED_TARGETS...} = ( new_vm_lib()(false) )
      }

      # say "lib needs recompile: #{lib_nrc}"

      const recompile_lib = need_recompile(blob_stats, :lib)
      # NOTE: compiling only the lib can be done in parallel with the next step
      const lib_thread = (recompile_lib ? new_vm_lib() : {}).ffork(true)
      # next, if the image is missing, rebuild it
      # bp(__FILE__, __LINE__, __NAMESPACE__, self.class, __METHOD_NAME__, blob_stats_image => dump(blob_stats{:image}))

      if ((!blob_stats{:image}.exists) || self.spoof_build) {
        blob_stats{:image} = BlobStatus(
          exists:     self.compile_image(bo),
          recompiled: true
        )
      }

      # don't update the database's blob sums if the blob wasn't recompiled

      recompile_lib && (blob_stats{Factor::CHECKSUMMED_TARGETS...} = (lib_thread.wait))
      const disk_blob_sums = BlobSums(
        algo: self._algo(bo),
        blob_stats-:(:image) -> pairs.map{
          .first ⫶ Maybe::unmaybe( .second.checksum )
        }...
      )

      # self.log.vbs(__FILE__, __LINE__, MN, "NOTE: TWICE: #{new_recompiled}")
      self.log.trc(__FILE__, __LINE__, MN, "done calculating checksums / rebuilding")

      # if they STILL don't match, delete the files + tombstone the entry and quit
      if ( (bo.blob_sums !~ disk_blob_sums) && (!self.spoof_build) ) {
        self.log.trc(__FILE__, __LINE__, MN, 'blob sums still mismatched!')
        return if (is_restart) {
          self.log.wrn(__FILE__, __LINE__, MN, "i give up (already in a restart)")
          false
        } else {
          self.log.wrn(__FILE__, __LINE__, MN, "starting over...")
          self._tombstone_entry(bo)
          __METHOD__(is_restart: true)
        }
      }

      # TODO: audit this logic
      # going to make a new updated BuiltObj from match_bo
      const new_bo = bo.cleave( { .lock }, { .dclone.unlock } ).last
      if ( blob_stats.values.any{ .recompiled } ) {
        self.log.vbs(__FILE__, __LINE__, MN, 'writing memory BuiltObj because of recompiled objects')
        new_bo.blob_sums(disk_blob_sums)
        new_bo.timespec.modified(Time.now)
      }
      new_bo.lock
      if (new_bo != bo) {
        self.log.trc(__FILE__, __LINE__, MN, 'committing changed BuiltObj')
        self.db_man.change( new_bo )
      }
      # and then run Factor
      self.log.trc(__FILE__, __LINE__, MN, "ok, running factor!")
      self._detach_factor_thread( bo.vm_fullname )
    }
    # multifactor's actual entry point
    # FAR AWAY TODO: make this asynchronous, so that with_directory stuff is
    # done in a separate actor and awaited

    # TODO: dynamic hash algo selection
    # TODO: read mtimes
    # TODO: test whether the images are older than the file mtimes?!
    # TODO: write mtime/atimes
    # TODO: testability
    # TODO: image compression?

    # still needs a rewrite
    method go {
      const MN = __METHOD_NAME__
      if (! Set('run-factor', 'spoof-exists').has(self.action)) {
        given (self.action) {
          when ('src-sums') { self.fbash.say_src_sums }
          when ('env-info') { self.fbash.say_env_info }
          # when ('class-info') { self.show_info }
        }
        return ()
      }

      const spoof_exists = self.action=='spoof-exists'

      # property names in BuiltObj, used to create current_status
      # BuildSh and FactorBash implement these through FactorBash.delegate
      define Set status_properties = Set(
        :factor_version, :is_git, :git_branch, :git_hash,
        :os_type, :os_name, :arch_name, :word_size,
        :vm_format, :image_format, :lib_format, :boot_format,
        :blob_algorithm, :src_sums
      )

      const BuiltObj current_status = BuiltObj().cleave(
        { .blob_algorithm( self.algos.blob ) }
        { status_properties.each{ |m|
            with ( self.fbash.delegate( m ) ) { |v| .method( m )( v ) }
        } }
        { .lock }
      ).last
      # if (self.debug) { #say current_status.dump #say db_data.dump }

      const match_bo = (self.db_man.data.values.find{
          |b|
        # say "find b: #{b.dump}"
        # remember, we're not testing *all* properties like .lock or tombstoned
        current_status._serial_eq(b, incl: status_properties)
      } \\> :lock)

      bp(__FILE__, __LINE__, __NAMESPACE__, self.class, __METHOD_NAME__, match_bo => match_bo)#, data => self.db_man.data)

      with (match_bo \\ ( spoof_exists ? current_status : nil )) {
          |mb|
        self.cleave(
          ( !(Set(:never, 0, nil).has( mb.timespec.tombstoned ) || spoof_exists)
            ? ( { ._tombstone_entry(mb) }
                { .build_new_factor(current_status) } )
            : ( { .log.vbs(__FILE__, __LINE__, MN, 'matching object in db') }
                { .update_existing_factor(mb, current_status) } ) )
        ).last
      } else {
        say '!!! build new factor!'
        self.build_new_factor(current_status)
      }
    }

    -> _detach_factor_thread (String vm_name) {
      const MN = __METHOD_NAME__
      {
        self.log.inf(__FILE__, __LINE__, MN, 'Launching Factor: ', "'", vm_name, self.factor_arguments..., "'")
        if ( self.fbash.with_directory{
            say `stat ./#{vm_name}`
            Command::spawn_se('./' + vm_name, self.factor_arguments..., output: false)
        } ) {
          self.log.wrn(__FILE__, __LINE__, MN, "Failed!")
        }
      }.thr.detach
      # don't segfault
    }
  }
}

define MICRO_MAGNITUDE = 1_000
func μsec { Time.micro*MICRO_MAGNITUDE }
func μsec (Number n) { Time.micro*MICRO_MAGNITUDE - n }

func main {
  const μs = μsec()
  μs
  # say Factor::RC_FILE
  # say Factor::REPO
  # say Factor::VERSION
  # say Dir.cwd
  # say Dir.cwd
  # Dir.chdir(Factor::REPO)
  # say BuildSh::generate_make_targets()
  # say FactorBash::sum_git_names_matching(/\.s(?:m|f)/)
  # say FactorBash::sum_git_contents_matching(/\.s(?:m|f)/)
  # say MultiFactor::BuiltObj(([""] * 15)..., false ).dump.len

  const parser = providers::argparse::ArgParse(ARGV) \
    .info(SCRIPT) \
    .option(:force, :f, doc: 'Force a rebuild in any case') \
    .option(:trace, :t, doc: 'Some debug tracing') \
    .option(:verbose, :V, doc: 'Verbose debug tracing') \
    # .option(:info, :i, doc: 'Just show configuration information') \
    .option('dry-run', :n, doc: 'Change no disk files') \
    .option('from-scratch', :g, doc: 'ignore existing DB (debug)') \
    # .option('sums', :c,  negate: (:Always ¦ :Never), doc: 'use checksums') \
    # TODO
    .option('sum-algo', :k,  param: (:algo ¦ :sha256), doc: 'Checksum algorithm to use') \

    .option('db-name', :D, param: (:name ¦ '.multifactor.db'), doc: 'Custom database filename') \
    .option('db-dir', :d, param: (:dir ¦ '.'), doc: 'Directory containing database') \
    .option('factor-dir', :w, param: (:dir ¦ "."), doc: 'Path to Factor working directory') \
    .option('remove-old-lock', :R, doc: 'Blindly remove any lock files (unsafe!)') \
    .option('quiet-subcom', :q, negate: (:Never ¦ :Always), doc: 'show subcommand outputs') \
    # TODO: argparse can accept a list of allowed values
    .option(
      :action, :a, param: (:name ¦ 'run-factor'),
      doc: "Perform this action\nactions: run-factor, src-sums, env-info"
    ) \
    .option('basis-dev', :b, doc: "Ignore changes in basis/\n(basis development mode)") \
    .option('spoof-build', :s, doc: "Don't actually compile or build anything") \
    .use_option_terminator('Further arguments are given to Factor') \
    # .use_stdin_file_arg \
    # .use_arg_list_terminator \
    # .ignore_unknowns \
    # .help_on_stderr \
    .arg("arg...", doc: "Arguments to the final Factor VM")

  var (opts, args, *_) = parser.go...
  # say "#{opts} #{args} #{_}"

  const destroy_mf = Combi::FinalizerSpec(
    obj: MultiFactor::MFRuntime(opts, args.keys).debug(true),
    shared_f7rs: [[:destroy]],
    exc_only: false
  )

  const done = Combi::with_destructors( destroy_mf )({
    .go
  })
  # say Combi::dump(done)
  with (done) {
   if (!.success) {
     Combi::rescope( lib::msg_classes::classify( .{:exc_info}{:msg} ) )({
         |type, fields|
       Factor::say_stderr('red', "~MFRuntime called due to runtime error: #{type} :: #{fields.grep_kv{ .!~ /_tried_invoke/ }}\n") #
     })
     # #{Combi::dump(_)}
   }
  }

  Factor::say_stderr('bold bright_white', "\n\tΔ#{μsec(μs)} ×1,000μ")
}

if (__FILE__ == __MAIN__) {
  with ( Feature::ANSIColor ) {
    say ( .color('bright_green'), SCRIPT{:name}, ' entrypoint main()', .RESET)
  }
  main()
}
